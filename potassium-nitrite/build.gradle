/*
 *
 * Copyright 2017-2018 Nitrite author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
}

jar {
    baseName = 'potassium-nitrite'
}

dependencies {
    compile project(':nitrite')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile project (path: ':nitrite', configuration: 'testArchives')
    testCompile "org.threeten:threetenbp:1.3.6"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    includes = ['package.md']
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

test {
    testLogging.showStandardStreams = false
    testLogging.exceptionFormat = 'full'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

nexusStaging {
    username = System.getenv('OSSRH_USER')
    password = System.getenv('OSSRH_PASSWORD')
}

uploadArchives {
    onlyIf {
        project.hasProperty('release') || version.endsWith("SNAPSHOT")
    }

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('OSSRH_USER'),
                        password: System.getenv('OSSRH_PASSWORD'))
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: System.getenv('OSSRH_USER'),
                        password: System.getenv('OSSRH_PASSWORD'))
            }

            pom.project {
                packaging 'jar'
                name "Potassium Nitrite"
                description 'An in-memory, single-file based embedded nosql persistent document store for Kotlin.'
                url 'https://github.com/dizitart/nitrite-database'
                scm {
                    url 'git@github.com:dizitart/nitrite-database.git'
                    connection 'scm:git:git@github.com:dizitart/nitrite-database.git'
                    developerConnection 'scm:git:git@github.com:dizitart/nitrite-database.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'anidotnet'
                        name 'Anindya Chatterjee'
                        email 'anidotnet@gmail.com'
                        organization 'Dizitart'
                    }
                }
            }
        }
    }
}
